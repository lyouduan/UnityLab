#pragma kernel FrustumCulling 

uint instanceCount;
StructuredBuffer<float4x4> positions;
float4 planes[6];
AppendStructuredBuffer<float4x4> cullingResults;

bool IsOutsideThePlane(float4 plane, float3 pointPosition)
{
    if (dot(plane.xyz, pointPosition) + plane.w > 0)
        return true;
    return false;
}

[numthreads(512,1,1)]
void FrustumCulling(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= instanceCount)
        return;
    
    float4x4 InsPosition = positions[id.x];
    
    // Bounding box
    float3 boundMin = float3(-1.5, 0, -1.5);
    float3 boundMax = float3(1.5, 7, 1.5);
    float4 boundVerts[8]; //AABB8个顶点坐标
    boundVerts[0] = mul(InsPosition, float4(boundMin, 1));
    boundVerts[1] = mul(InsPosition, float4(boundMax, 1));
    boundVerts[2] = mul(InsPosition, float4(boundMax.x, boundMax.y, boundMin.z, 1));
    boundVerts[3] = mul(InsPosition, float4(boundMax.x, boundMin.y, boundMax.z, 1));
    boundVerts[6] = mul(InsPosition, float4(boundMax.x, boundMin.y, boundMin.z, 1));
    boundVerts[4] = mul(InsPosition, float4(boundMin.x, boundMax.y, boundMax.z, 1));
    boundVerts[5] = mul(InsPosition, float4(boundMin.x, boundMax.y, boundMin.z, 1));
    boundVerts[7] = mul(InsPosition, float4(boundMin.x, boundMin.y, boundMax.z, 1));

    for (int i = 0; i < 6; i++)
    {
        for (int j = 0; j < 8; j++)
        {
            float3 boundPosition = boundVerts[j].xyz;

            if (!IsOutsideThePlane(planes[i], boundPosition))
                break;
            if (j == 7)
                return;
        }
    }
        

    cullingResults.Append(InsPosition);
}
